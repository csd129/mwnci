if (!INCLUDE["PS"]) {
    INCLUDE=set(INCLUDE, "PS", true)
    
    function process() {
        CmdHash={}
        ProcessTable={}
        Processes=split(trim(system("ps aux | grep -v \"\\[\" | tail +2")), "\n")
        foreach process in Processes {
            Cmd=""
            line=fields(process)
            b=shift(line, 10)
            Cmd=trim(join(b, " "))
    	    CmdHash={"USER": line[0], "CPU": line[2], "MEM": line[3], "VSZ": line[4], "RSS": line[5], "TTY": line[6], "STAT": line[7], "START": line[8], "TIME": line[9], "COMMAND": Cmd}
    	    ProcessTable=set(ProcessTable, line[1], CmdHash)
        }
        return ProcessTable
    }
    
    function pnametopid(name="") {
        if (!checkfunctype("pnametopid", "string", name, 1)) {exit(1)}        
        parray=[]
        if (name=="") {
            println("Error calling `pnametopid`: ERROR: TypeError: pnametopid() expected argument #1 to be `STRING` got `NULL`")
            exit(1)
        }
        Processes=process()
        PIDs=sort(keys(Processes))
        foreach Pid in PIDs {
            CommandLine=Processes[Pid]["COMMAND"]
    	    CmdArray=fields(CommandLine)
    	    Command=basename(CmdArray[0])
            if (Command == name) {
    	        parray=push(parray, Pid)
    	    }
        }
        if (len(parray) > 0) {
            return parray
        }
        return false
    }
    
    function getpname(pid=0) {
        if (!checkfunctype("getpname", "integer", pid, 1)) {exit(1)}
        if (pid == 0) {pid=getpid()}
        Processes=process()
        Pname=Processes[string(pid)]["COMMAND"]
        if (Pname) {
            Pname=basename(fields(Pname)[0])
            return Pname
        }
        return false
    }
    
    function procstat(pid=-1) {
        if (!checkfunctype("procstat", "integer", pid)) {exit(1)}
        Data={}
        switch (OS) {
            case "freebsd" {
                Command="ps -axo pcpu,pmem,pid,user,args | tail +2"
            }
            default {
                Command="ps -eo pcpu,pmem,pid,user,args --no-headers"
            }
        }
        Result=split(trim(system(Command)), "\n")
        foreach Line in Result {
            SLine=fields(Line)
            if (len(SLine) > 0) {
                if (index(SLine[4], "[") == -1) {
                    Proc_Hash={"pcpu": SLine[0], "pmem": SLine[1], "user": SLine[3], "cmd": SLine[4]}
                    Key=SLine[2]
                    Data=set(Data, Key, Proc_Hash)
                }
            }
        }
        if (pid > 0) {
            return Data[string(pid)]
        } else {
            return Data
        }
    }
    
    function integer.getpname() {return getpname(self)}
    function string.pnametopid() {return pnametopid(self)}
    function integer.procstat() {return procstat(self)}
}
