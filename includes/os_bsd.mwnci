if (!INCLUDE["OSBSD"]) {
    INCLUDE=set(INCLUDE, "OSBSD", true)
    if (!INCLUDE["STRING"]) {
        include("string")
    }
    
    function free() {
        Swap=split(trim(system("swapinfo")), "\n")
        SwapData=fields(Swap[1])
        SwapSize=int(SwapData[1])
        SwapUsed=int(SwapData[2])
        SwapFree=int(SwapData[3])
        Mem=memstat()
        MemHash={"mem": {"buffcache": Mem["buffers"] / 1024, "available": Mem["available"] / 1024, "total": Mem["total"] / 1024, "used": Mem["used"] / 1024, "free": Mem["free"] / 1024}}
        SwapHash={"free": int(SwapFree), "total": int(SwapSize), "used": int(SwapUsed)}
        MemHash=set(MemHash, "swap", SwapHash)
        return MemHash
    }

    function getcpu(CpuArg="") {
    if (!checkfunctype("getcpu", "string", CpuArg, 1)) {exit(1)}
//
// Gotta do 2 (-d2) runs to get a more accuraterer view of the cpu's :(
//
        Data=split(trim(system("top -d2 -P p")), "\n")
        UserCount=0
        NiceCount=0
        SysCount=0
        IntCount=0
        IdleCount=0
        Total=0
        CPUHash={}
        foreach Line in Data {
            if (match("^CPU", Line)) {
                Total++
                Sline=fields(Line)
                Cpu=trim(Sline[1], ":")
                User=float(trim(Sline[2], "%"))
                UserCount += User
                Nice=float(trim(Sline[4], "%"))
                NiceCount += Nice
                Sys=float(trim(Sline[6], "%"))
                SysCount += Sys
                Int=float(trim(Sline[8], "%"))
                IntCount += Int
                Idle=float(trim(Sline[10], "%"))
                IdleCount += Idle
                CPU="cpu" + Cpu
                CPUHash=set(CPUHash, CPU, {"user": User, "nice": Nice, "system": Sys, "interrupt": Int, "idle": Idle})
            }
        }
        User=float(sprintf("%.2f", UserCount / Total))
        Nice=float(sprintf("%.2f", NiceCount / Total))
        Sys=float(sprintf("%.2f", SysCount / Total))
        Int=float(sprintf("%.2f", IntCount / Total))
        Idle=float(sprintf("%.2f", IdleCount / Total))
        AllHash={"user": User, "nice": Nice, "system": Sys, "interrupt": Int, "idle": Idle}
        CPUHash=set(CPUHash, "all", AllHash) 
        if (CpuArg != "" && len(CPUHash) > 0 ) {
            CPUHash=CPUHash[CpuArg]
	    CPUHash=set(CPUHash, "cpu", CpuArg)
        }
        return CPUHash
    }

    function datetoepoch(Date="", Format="") {
        if (!checkfunctype("datetoepoch", "string", Date)) {exit(1)}
        if (!checkfunctype("datetoepoch", "string", Format)) {exit(1)}
        if (Date == "") {
            Cmd="`date +%s`"
        } else {
            Cmd=sprintf("`date -j -f \"%s\" \"%s\" +%%s`", Format, Date)
        }
        Result=eval(Cmd)
        if (len(Result["stderr"]) > 0) {
            println(Result["stderr"])
            return NULL
        }
        return int(trim(Result["stdout"]))
    }

    function vmstat() {
        Data=system("vmstat -H")
        Data=tail(split(trim(Data), "\n"), 1)
        DataHash={}
        Info=fields(Data[0])
        ProcHash={"Run": int(Info[0]), "Block": int(Info[1]), "Swapd": int(Info[2])}
        MemHash={"Virt": int(Info[3]), "Free": int(Info[4])}
        PageHash={"Fault": int(Info[5]), "React": int(Info[6]), "PageIn": int(Info[7]), "PageOut": int(Info[8]), "Freed": int(Info[9]), "Scan": int(Info[10])}
        FaultHash={"In": int(Info[13]), "Sys": int(Info[14]), "Cntxt": int(Info[15])}
        CPUHash={"User": int(Info[16]), "Sys": int(Info[17]), "Idle": int(Info[18])}
        DataHash={"Procs": ProcHash, "Memory": MemHash, "Page": PageHash, "Fault": FaultHash, "CPU": CPUHash}
        return DataHash
    }


}
