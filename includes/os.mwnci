if (!INCLUDE["OS"]) {
    INCLUDE=set(INCLUDE, "OS", true)

//
// The functions in here cater for all distro's (that I have access to)
// The next few lines will also "include"  distro specific functions

    switch (OS) {
        case /bsd/ , /darwin/ {
            include("os_bsd")
        }
        case "linux" {
            include("os_linux")
        }
    }

function uname() {
        Data=fields(trim(system("uname -n -s -r -m")))
        DataHash={
            "nodename": Data[0], "sysname": Data[1],
            "release": Data[2], "machine": Data[3]
        }
        return DataHash
    }

    function df() {
        DiskHash={}
        Mounted=shift(split(system("df"),"\n"))
        Length=len(Mounted)-1
        Count=0
        while (Count <= Length) {
            Entry=Mounted[Count]
            Next=Mounted[Count+1]
            if (len(Entry) > 0) {
                SplitLine=fields(Entry)
//
// check for long mount length.. Disk info will be on line Count + 1
//
                if (len(SplitLine) == 1) {
                    NSplitLine=fields(Next)
                    SplitLine=extend(SplitLine, NSplitLine)
                    Count++
                }

                if (SplitLine[0].contains("/")) {
                    PercentUsed=int(trimsuffix(SplitLine[4], "%"))
                    PercentFree=100 - PercentUsed
                    TempHash={"blocks": int(SplitLine[1]), "used": int(SplitLine[2]), "available": int(SplitLine[3]), "usedperc": PercentUsed, "freeperc": PercentFree, "mount": SplitLine[5]}
                    DiskHash=set(DiskHash, SplitLine[0], TempHash)
                }
            }
            Count++
        }
        return DiskHash
    }

    function osrelease() {
        OsrHash={
            "name": null,
            "version": null,
            "id": "linux",
            "id_like": [],
            "version_codename": null,
            "version_id": null,
            "pretty_name": null,
            "ansi_color": null,
            "cpe_name": null,
            "build_id": null,
            "variant": null,
            "variant_id": null,
            "logo": null
        }

        UrlHash={
            "home": null,
            "documentation": null,
            "support": null,
            "bug_report": null,
            "privacy_policy": null
	    }
  	    DataFile=""
	    if (isexist("/etc/os-release")) {
	        DataFile="/etc/os-release"
	    } else if (isexist("/usr/lib/os-release")) {
	        DataFile="/usr/lib/os-release"
        }

        if (DataFile == "") {
            return OsrHash
        }
        Data=split(trim(cat(DataFile)), "\n")
        foreach Line in Data {
            Sline=split(Line, "=")
            Key=tolower(Sline[0])
            Value=trim(Sline[1], "\"")
            if (match("_url", Key)) {
                Key=trimsuffix(Key, "_url")
                UrlHash=set(UrlHash, Key, Value)
            } else {
                if (match("id_like", Key)) {Value=fields(Value)}
                OsrHash=set(OsrHash, Key, Value)
            }
        }
        OsrHash=set(OsrHash, "urls", UrlHash)
        return OsrHash
    }

    function loadavg() {
        Data=fields(trim(system("uptime")))
	Last=len(Data)-1
	fifteen=float(trim(Data[Last],","))
	five=float(trim(Data[Last-1], ","))
	one=float(trim(Data[Last-2], ","))
	return [one, five, fifteen]
    }

    function string.datetoepoch(Format="") {
        return datetoepoch(self, Format)

    }
}

