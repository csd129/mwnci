if (!INCLUDE["OS"]) {
    INCLUDE=set(INCLUDE, "OS", true)
   
    function hostinfo() {
        Data=fields(trim(system("uname -n -s -r -m")))
        DataHash={
            "nodename": Data[0], "sysname": Data[1],
            "release": Data[2], "machine": Data[3]
        }
        return DataHash
    }

    function env() {
        h={}
        env_vars=split(system("env"), "\n")
        foreach ev in env_vars {
            v=cut(ev, "=")
            h=set(h, v[0], v[1])
        }
        return h
    } 
    
    function syslog(priority="syslog.info", message="") {
        if (!checkfunctype("syslog", "string", priority, 1)) {exit(1)}
        if (!checkfunctype("syslog", "string", message, 2)) {exit(1)}
        if (message == "") {
            println("'syslog' requires a message, none given")
            return false
        }
        cmd=sprintf("`logger -p %s \"%s\"`", priority, message)
        result=eval(cmd)["stdout"]
        return result
    }
    
    function df() {
        diskhash={}
        mounted=shift(split(system("df"),"\n"))
        foreach entry in mounted {
            if (len(entry) > 0) {
                splitline=fields(entry)
	            if (splitline[0].contains("/")) {
                    perc_used=int(trimsuffix(splitline[4], "%"))
                    perc_free=100 - perc_used
                    temphash={"blocks": int(splitline[1]), "used": int(splitline[2]), "available": int(splitline[3]), "usedperc": perc_used, "freeperc": perc_free, "mount": splitline[5]}
                    diskhash=set(diskhash, splitline[0], temphash)
                }
            }
        }
        return diskhash
    }

    function free() {
        memhash={}
	switch (OS) {
	    case "linux" {
	        mem=meminfo()
		mem_total=int(mem["MemTotal"])
		mem_free=int(mem["MemFree"])
		mem_available=int(mem["MemAvailable"])
		mem_shared=int(mem["Shmem"])
		mem_cached=int(mem["Cached"]) + int(mem["SReclaimable"])
		mem_bufcache=int(mem["Buffers"]) + mem_cached
		mem_used=mem_total - mem_available
		mem_used_perc=sprintf("%.2f", float(mem_used) / float(mem_total)*100)
		mem_free_perc=sprintf("%.2f", float(100.00 - float(mem_used_perc)))
		swap_total=int(mem["SwapTotal"])
		swap_free=int(mem["SwapFree"])
		swap_used=swap_total - swap_free
		swap_used_perc=sprintf("%.2f", float(swap_used) / float(swap_total)*100)
		swap_free_perc=sprintf("%.2f", float(100.0 - float(swap_used_perc)))

		hashy={"buff_cache": mem_bufcache, "cache": mem_cached, "shared": mem_shared, "total": mem_total, "free": mem_free, "available": mem_available, "used": mem_used, "used_perc": mem_used_perc, "free_perc": mem_free_perc}
		memhash=set(memhash, "mem", hashy)

		hashy={"total": swap_total, "free": swap_free, "used": swap_used, "used_perc": float(swap_used_perc), "free_perc": float(swap_free_perc)}
		memhash=set(memhash, "swap", hashy)
	    }    
	    case "freebsd" {
	        sysctl_output=`/sbin/sysctl -a | egrep `
		sysctl_cmd="/sbin/sysctl -a | egrep \"vm.stats.vm.v|hw.physmem|hw.pagesize\""
		sysctl_output=split(system(sysctl_cmd),"\n")
		sysctl={}
		foreach Line in sysctl_output {
		    Sline=cut(Line, ": ")
		    sysctl=set(sysctl, Sline[0], Sline[1])
	    	}
		page_size=int(sysctl["hw.pagesize"])
		mem_hw = int(sysctl["hw.physmem"])
		mem_phys= int(sysctl["hw.physmem"])
		mem_all=int(sysctl["vm.stats.vm.v_page_count"])  * page_size
		mem_wire=int(sysctl["vm.stats.vm.v_wire_count"]) * page_size
		mem_active=int(sysctl["vm.stats.vm.v_active_count"]) * page_size
		mem_inactive=int(sysctl["vm.stats.vm.v_inactive_count"]) * page_size
		mem_cache=int(sysctl["vm.stats.vm.v_cache_count"]) * page_size
		mem_free=int(sysctl["vm.stats.vm.v_free_count"]) * page_size

		mem_total = mem_hw
		mem_avail = mem_inactive + mem_cache + mem_free
		mem_used = mem_total - mem_avail

		hashy={"mem_cache": mem_cache / 1024, "total": mem_all / 1024, "mem_wire": mem_wire / 1024, "mem_active": mem_active / 1204, "mem_inactive": mem_inactive / 1024, "mem_free": mem_free / 1024}
		memhash=set(memhash, "mem", hashy)
            }
        }
        return memhash
    }

    function datetoepoch(datey="") {
        if (!checkfunctype("datetoepoch", "string", datey)) {
            exit(1)
        }
        if (datey == "") {
            cmd="date +%s"
        } else {
            cmd="date +%s -d \"" + datey + "\""
        }
        result=system(cmd)
        secs=int(trim(result))
        return secs
    }

    function meminfo() {
        memhash={}
        data=split(cat("/proc/meminfo"), "\n")
        foreach entry in data {
            if (len(entry) > 0) {
                entry=replaceall(entry, ":", "")
                entryarray=fields(entry)
                memhash=set(memhash, entryarray[0], entryarray[1])
            }
        }
        return memhash
    }
    
    function string.datetoepoch() {
        return datetoepoch(self)
    }
}


