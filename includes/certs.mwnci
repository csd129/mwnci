include(
    "os"
)
    
if (!INCLUDE["CERTS"]) {
    INCLUDE=set(INCLUDE, "CERTS", true)
    
    function getcert(Domain="", TimeOut=5) {
        if (!checkfunctype("getcert", "string", Domain)) {
            exit(1)
        }
        if (!checkfunctype("getcert", "integer", TimeOut, 2)) {
            exit(1)
        }
        if (len(Domain) == 0) {
            println("`getcert` expects a domain name, none given")
    	    return false
        }
        Command=sprintf("timeout %ds openssl s_client -servername %s -connect %s:443 2>&1; exit 0", TimeOut, Domain, Domain)
	Result=system(Command)
	return contains(Result, "ERROR") ? NULL : Result
    }
    
    function certexpiredays(File="") {
        if (!checkfunctype("certexpiredays", "string", File)) {
            exit(1)
        }
        CertFile=tmpnam("/tmp")
        if (len(File) == 0) {
            return false
        }
        if (!isreadable(File)) {
            println("Error calling `certexpiredays`: ERROR: OpenError : Can't open file ", File)
            exit(1)
        }
        Cert=getfileblock("-BEGIN CERTIFICATE-", "-END CERTIFICATE-", File)
        writetofile(CertFile, "w", Cert)
        Command=sprintf("openssl x509 -in %s -enddate -noout", CertFile)
        CertDate=system(Command)
        CertDate=trim(split(CertDate, "=")[1])
        switch (OS) {
            case /bsd/ , /darwin/ {
                CertEpoch=datetoepoch(CertDate, "%b %e %T %Y %Z")
            }
            case "linux" {
                CertEpoch=datetoepoch(CertDate)
            }
        }
        unlink(CertFile)
        return (CertEpoch - time()) / 86400
    }
    
    function sitecertexp(Domain="") {
        if (!checkfunctype("sitecertexp", "string", Domain)) {
            exit(1)
        }
        TEMPF=tmpnam("/tmp")
        CertInfo=getcert(Domain)
        if (len(CertInfo) == 0) {
            return 0
	}
        writefile(TEMPF, CertInfo)
        CertDate=certexpiredays(TEMPF)
        unlink(TEMPF)
        return CertDate
    }

    function certsans(File="") {
        if (!checkfunctype("certsans", "string", File)) {exit(1)}
        if (len(File) == 0) {
            return false
        }
        if (!isreadable(File)) {
            println("Error calling `certsans`: ERROR: OpenError : Can't open file ", File)
            exit(1)
        }
	TempFile=tmpnam("/tmp")
	Command=sprintf("openssl x509 -in %s -text -noout -dates 2>&1; exit 0", File)
	writetofile(TempFile, "w", system(Command))
        Found=[]
	Data=regexp("DNS:([^\s,]+)", TempFile)
        foreach Line in Data {
            Line=replaceall(Line, "DNS:", "")
            Line=fields(replaceall(Line, ",", ""))
            Found=extend(Found, Line)
        }
        unlink(TempFile)
        return sort(uniq(Found))
    }

    function sitecertsans(Domain="") {
        if (!checkfunctype("sitecertsans", "string", Domain)) {
            exit(1)
        }
        TempFile=tmpnam("/tmp")
        CertInfo=getcert(Domain)
        writefile(TempFile, CertInfo)
        CertSans=certsans(TempFile)
        unlink(TempFile)
        return CertSans
    }
}
