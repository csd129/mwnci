if (!INCLUDE["OS"]) {include("os")}
    
if (!INCLUDE["CERTS"]) {
    INCLUDE=set(INCLUDE, "CERTS", true)
    
    function getcert(Domain="") {
        if (!checkfunctype("getcert", "string", Domain)) {
            exit(1)
        }
        if (len(Domain) == 0) {
            println("`getcert` expects a domain name, none given")
    	    return false
        }
        Command=sprintf("openssl s_client -servername %s -connect %s:443", Domain, Domain)
        return system(Command)
    }
    
    function certexpiredays(File="") {
        if (!checkfunctype("certexpiredays", "string", File)) {
            exit(1)
        }
        CertFile=tmpnam("/tmp")
        if (len(File) == 0) {
            return false
        }
        Cert=getfileblock("-BEGIN CERTIFICATE-", "-END CERTIFICATE-", File)
	writetofile(CertFile, "w", Cert)
        Command=sprintf("openssl x509 -in %s -enddate -noout", CertFile)
        CertDate=system(Command)
        CertDate=trim(split(CertDate, "=")[1])
        switch (OS) {
            case "linux" {CertEpoch=datetoepoch(CertDate)}
            case "freebsd" {CertEpoch=datetoepoch(CertDate, "%b %d %T %Y %Z")}
        }
        unlink(CertFile)
        return (CertEpoch - time()) / 86400
    }
    
    function sitecertexp(Domain="") {
        if (!checkfunctype("sitecertexp", "string", Domain)) {
            exit(1)
        }
        TEMPF=tmpnam("/tmp")
        CertInfo=getcert(Domain)
        writefile(TEMPF, CertInfo)
        CertDate=certexpiredays(TEMPF)
        unlink(TEMPF)
        return CertDate
    }
}
